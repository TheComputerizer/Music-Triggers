//file:noinspection GroovyAssignabilityCheck
buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'gradle.plugin.com.github.johnrengelman', name: 'shadow', version: '7.1.2'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'

version = '1.18.1-5.0-EX'
group = 'mods.thecomputerizer.musictriggers'
archivesBaseName = 'musictriggers'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

mixin {
    add sourceSets.main, "musictriggers.refmap.json"
    config "musictriggers.mixins.json"
}

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: '1.18.1'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'examplemod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        url = 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    shade
    implementation.extendsFrom(shade)
    shadow.extendsFrom shade
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.1-39.0.85'

    shade ("com.moandjiezana.toml:toml4j:0.7.2"){
        exclude group: 'com.google.code.gson', module: 'gson' // Provided by Minecraft
    }
    shade ("com.googlecode.soundlibs:jlayer:1.0.1.4")
    shade ("org.jcodec:jcodec:0.2.5")
    shade ("org.jcodec:jcodec-javase:0.2.5")
    shade("io.github.kostaskougios:cloning:1.10.3")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    implementation fg.deobf("curse.maven:enhanced-celestials-438447:3544493")
    implementation fg.deobf("curse.maven:serene-seasons-291874:3599020")
    implementation fg.deobf("curse.maven:atomicstrykers-infernal-mobs-227875:3556335")
}

shadowJar {
    //necessary relocation
    //relocate 'org.slf4j', 'mods.thecomputerizer.musictriggers.libraries.sljf4'
    //relocate 'org.apache.commons', 'mods.thecomputerizer.musictriggers.libraries.apache.commons'
    //relocate 'org.apache.http', 'mods.thecomputerizer.musictriggers.libraries.apache.http'
    //not strictly necessary relocation
    relocate 'junit', 'mods.thecomputerizer.musictriggers.libraries.junit'
    relocate 'net.sourceforge', 'mods.thecomputerizer.musictriggers.libraries.sourceforge'
    relocate 'javazoom.jl', 'mods.thecomputerizer.musictriggers.libraries.jl'
    relocate 'com.alibaba', 'mods.thecomputerizer.musictriggers.libraries.alibaba'
    configurations = [project.configurations.shadow]
    classifier ''
}


reobf {
    shadowJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.output
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "musictriggers",
                "Specification-Vendor"    : "The_Computerizer",
                "Specification-Version"   : "5.0-EX",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "The_Computerizer",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}
tasks.assemble.dependsOn shadowJar